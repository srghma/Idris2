default: build

clean:
    rm -f support-sep.ss

install destdir idris2_version: build
    mkdir -p {{destdir}}/idris2-{{idris2_version}}/support/chez
    install -m 644 *.ss {{destdir}}/idris2-{{idris2_version}}/support/chez

build:
    #!/usr/bin/env bash
    set -euo pipefail

    # Start library header
    echo "(library (support) (export" > support-sep.ss

    # Extract function names after 'define' statements and print the list of exports
    cat support.ss \
       | sed -n 's|(define (\?\([^ )]*\).*|\1|p' \
       >> support-sep.ss
    echo ") (import (chezscheme))" >> support-sep.ss

    # Skip first line if it starts with shebang and copy the rest of the code
    sed '1{/^#!/d;}' support.ss >> support-sep.ss

    # Close the bracket
    echo ") ; end of (library)" >> support-sep.ss
